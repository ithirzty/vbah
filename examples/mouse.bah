/////////////////////////////////////////////////////////////
//        Mouse demo using OpenGL and GLUT in Bah.         //
//          To compile, use 'bah ./mouse.bah -d'.          //
//                                                         //
//                                     © Aloïs Laurent Boë //
/////////////////////////////////////////////////////////////


#include "iostream.bah"
#include "time.bah"
#include "rand.bah"

#include "../vbah.bah"

win window*
//Color of the circle
color rgb

//Circle position
pos vec

//Set the circle color to white when window is initiated
init() {
    pos = vec(0.0, 0.0)
    color = rgb(1.0, 1.0, 1.0)
}

display() {
    //Draw the circle every frame
    drawCircle(pos, 0.05, color)
}

click(button int, pressed bool, x int, y int) {
    //Translate the position of the event from pixels to window coordinates
    f = vec(<float>x, <float>y)
    pixelsToCoords(&f)
    //Update position
    pos.set(f)
    //Change the color if the button is pressed or released
    if pressed == true {
        color = rgb(1.0, 0.0, 0.0)
    } else {
        color = rgb(1.0, 1.0, 1.0)
    }

}

drag(x int, y int) {
    //Translate the position of the event from pixels to window coordinates
    f = vec(<float>x, <float>y)
    pixelsToCoords(&f)
    //Update position
    pos.set(f)
    //Set the color to green
    color = rgb(0.0, 1.0, 0.0)
}

move(x int, y int) {
    //Translate the position of the event from pixels to window coordinates
    f = vec(<float>x, <float>y)
    pixelsToCoords(&f)
    //Update position
    pos.set(f)
}

main(args []cpstring) int {
    //launching the window
    
    win = window(600, 600, "Mouse")
    win.display = display
    win.click = click
    win.drag = drag
    win.init = init
    win.move = move
    win.launch()

    return 0
}